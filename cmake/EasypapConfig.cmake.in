@PACKAGE_INIT@

set(_Easypap_supported_components ezv ezm)

# If no component has been requested, include them all
if (NOT Easypap_FIND_COMPONENTS)
  set(Easypap_FIND_COMPONENTS ${_Easypap_supported_components})
endif()

# ezm depends on ezv
if ("ezm" IN_LIST Easypap_FIND_COMPONENTS)
  if (NOT "ezv" IN_LIST Easypap_FIND_COMPONENTS)
    list (PREPEND Easypap_FIND_COMPONENTS "ezv")
    message (STATUS "Adding ezv to dependency list")
  endif()
endif()

include(CMakeFindDependencyMacro)

foreach(_comp ${Easypap_FIND_COMPONENTS})
  if (NOT _comp IN_LIST _Easypap_supported_components)
    set(Easypap_FOUND False)
    set(Easypap_NOT_FOUND_MESSAGE "Unsupported component: ${_comp}")
  endif()
  include("${CMAKE_CURRENT_LIST_DIR}/${_comp}Targets.cmake")
  include("${CMAKE_CURRENT_LIST_DIR}/${_comp}Config.cmake")
endforeach()
